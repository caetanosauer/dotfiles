" vimrc for hyper-specific settings

" indentation length in hyper is 3
set softtabstop=3
set shiftwidth=3

" don't wrap, since hyper has very long lines
set nowrap

" only execute commands below once for each vim instance, instead of once for each opened buffer
" see doc for g:localvimrc_event
if g:localvimrc_sourced_once
  finish
endif

let g:alternateSearchPath = 'reg:|dbcore|dbcore/include/dbcore|,reg:|dbcore/include/dbcore|dbcore|'

" Do not trigger YCM unless explicitly invoked
let g:ycm_auto_trigger = 0

" :CMakeToggle -- a command to toggle a variable g:cmake_target_dir between "debug" and "release"
if (!exists("g:cmake_target_dir"))
   let g:cmake_target_dir = "release"
endif
func! MyFuncCMakeToggle()
   if g:cmake_target_dir == "release"
      let g:cmake_target_dir = "debug"
   else
      let g:cmake_target_dir = "release"
   endif
   echo "CMake build dir is now: " . g:cmake_target_dir
endfunc
command! -nargs=0 CMakeToggle :call MyFuncCMakeToggle()

func! MyFuncNinjaCompile()
   exe 'set makeprg=ninja\ -C\ ' . g:cmake_target_dir
   :Make
endfunc
command! -nargs=0 NinjaCompile :call MyFuncNinjaCompile()
nnoremap <leader>xc :NinjaCompile<cr>

func! MyFuncRunHyperTests()
   exe 'FocusDispatch cd ' . g:cmake_target_dir . ' && ctest --verbose'
   :Dispatch!
endfunc
command! -nargs=0 RunHyperTests :call MyFuncRunHyperTests()
nnoremap <leader>xt :RunHyperTests<cr>
